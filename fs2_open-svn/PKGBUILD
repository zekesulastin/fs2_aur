# Maintainer: ZekeSulastin <zekesulastin@gmail.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=fs2_open-svn
pkgver=9440
pkgrel=1
pkgdesc="An enhancement of the original Freespace 2 engine - SVN version"
url="http://scp.indiegames.us"
arch=('i686' 'x86_64')
license=('custom:fs2_open')
depends=('libjpeg' 'libpng' 'libtheora' 'libvorbis' 'lua51' 'mesa' 'openal' 'sdl')
makedepends=('subversion')
provides=('fs2_open')
install=fs2_open-svn.install
source=('osapi_unix.patch'
		'increase_joy_buttons_fixed.patch'
		'fs2_open-svn')
md5sums=('783d5ab68a0ce4d26ee415e8fefbc762'
		'892cee11520d6e258eb17e897f98c1c9'
		'06cb23f05cf72435a844140d99f75c68')

_svntrunk=svn://svn.icculus.org/fs2open/trunk/fs2_open
_svnmod=fs2_open

build()
{
	cd "$srcdir"
	msg "Connecting to SVN server...."

	if [[ -d "$_svnmod/.svn" ]]; then
		(cd "$_svnmod" && svn up -r "$pkgver")
	else
		svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_svnmod-build"
	cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
	cd "$srcdir/$_svnmod-build"

	patch -Np0 -i "$srcdir/osapi_unix.patch"

	patch -Np0 -i "$srcdir/increase_joy_buttons_fixed.patch"

	# Add --enable-debug to make a debug build.  These are NOT meant for general play;
	#  only make a debug build if generating logs/bugreports.  This is true even for
	#  these SVN builds, if you're using a mod that requires them.
	LDFLAGS="-l:liblua.so.5.1 $LDFLAGS" CXXFLAGS="-I/usr/include/lua5.1 $CXXFLAGS" ./autogen.sh --enable-speech
	make
}

package () {
	cd "$srcdir/$_svnmod-build"

	install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	if [[ -x code/fs2_open_3.6.15 ]]; then
		install -D -m755 code/fs2_open_3.6.15 "$pkgdir/opt/fs2_open/fs2_open_3.6.15"
	else
		install -D -m755 code/fs2_open_3.6.15_DEBUG "$pkgdir/opt/fs2_open/fs2_open_3.6.15_DEBUG"
	fi
	install -D -m755 "$srcdir/fs2_open-svn" "$pkgdir/usr/bin/fs2_open-svn"
}

